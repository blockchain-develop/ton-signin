#include "imports/stdlib.fc";

int op::excesses() asm "0xd53276db PUSHINT";

cell load_data() inline {
    cell data = get_data();
    slice ds = data.begin_parse();
    if (ds.slice_bits() == 0) {
        return new_dict();
    } else {
        return data;
    }
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) {
        return ();
    }
    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);
    int user_id = in_msg_body~load_uint(256); ;; hash,uint256
    int now = now() / (24 * 60 * 60); ;; day

    cell data = get_data();
    slice ds = data.begin_parse();
    cell dic = ds.slice_bits() == 0 ? new_dict() : data;
    
    int user_checkin_counter = 0;
    int user_checkin_latest = 0;
    (slice payload, int success) = dic.udict_get?(256, user_id);
    if (success) {
        user_checkin_counter = payload~load_uint(64);
        user_checkin_latest = payload~load_uint(64);
    }
    throw_unless(98, user_checkin_latest < now); ;; only checkin once per day

    user_checkin_counter += 1;
    user_checkin_latest = now;
    cell user_state = begin_cell().store_uint(user_checkin_counter, 64).store_uint(user_checkin_latest, 64).end_cell();
    cell user_checkin_history = begin_cell().store_uint(user_id, 256).store_uint(user_checkin_latest, 64).end_cell();
    dic~udict_set(256, user_id, user_state.begin_parse());
    dic~udict_set(256, cell_hash(user_checkin_history), begin_cell().store_uint(1, 8).end_cell().begin_parse());
    set_data(dic);

    ;; refund ton
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) { ;; bounce, ignore
        return ();
    }
    slice sender_address = cs~load_msg_addr();
    slice response_address = sender_address;
    if ((response_address.preload_uint(2) != 0) & (msg_value > 0)) {
        var refund = begin_cell()
        .store_uint(0x10, 6)
        .store_slice(response_address)
        .store_coins(msg_value)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::excesses(), 32)
        .store_uint(query_id, 64)
        .end_cell();
        send_raw_message(refund, 2);
    }
    return();
}

(int, int) get_user_checkin(int user_id) method_id {
    cell dic = load_data();
    (slice payload, int success) = dic.udict_get?(256, user_id);
    if (success) {
        int user_checkin_counter = payload~load_uint(64);
        int user_checkin_latest = payload~load_uint(64);
        return (user_checkin_counter, user_checkin_latest);
    } else {
        return (0, 0);
    }
}

(int) check_user_checkin(int user_id, int day) method_id {
    cell dic = load_data();
    cell user_checkin_history = begin_cell().store_uint(user_id, 256).store_uint(day, 64).end_cell();
     (_, int success) = dic.udict_get?(256, cell_hash(user_checkin_history));
    if (success) {
       return 1;
    } else {
        return 0;
    }
}
